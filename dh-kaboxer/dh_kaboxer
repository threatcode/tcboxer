#! /usr/bin/perl

use strict;
use warnings;

use Debian::Debhelper::Dh_Lib;
use File::Copy qw/cp/;
use File::Copy::Recursive qw/dircopy/;
use File::Temp qw/tempdir/;

init();

for my $package (@{ $dh{DOPACKAGES} }) {
  autoscript($package, 'postinst', 'postinst-kaboxer', "s/#PACKAGE#/$package/g");
  autoscript($package, 'postrm', 'postrm-kaboxer', "s/#PACKAGE#/$package/g");
  my $tmp = tmpdir($package);
  install_dir("$tmp/usr/src/kaboxer");
  install_dir("$tmp/usr/src/kaboxer/$package");
  install_dir("$tmp/usr/share/kaboxer");
  my $tempdir = tempdir();
  dircopy("kaboxer-source", "$tempdir")
      if -e "kaboxer-source";
  cp("debian/$package.Dockerfile", "$tempdir/Dockerfile")
      if -e "debian/$package.Dockerfile";
  doit("kaboxer", "build", "$package", "$tempdir");
  doit("kaboxer", "save", "$package", "$tmp/usr/share/kaboxer/$package.tar");
  doit("rm", "-r", "$tempdir");
  dircopy("kaboxer-source", "$tmp/usr/src/kaboxer/$package")
      if -e "kaboxer-source";
  install_file("debian/$package.Dockerfile", "$tmp/usr/src/kaboxer/$package/Dockerfile")
      if -e "debian/$package.Dockerfile";
  install_dir("$tmp/etc/kaboxer");
  install_file("debian/$package.Kaboxerfile", "$tmp/etc/kaboxer/$package.Kaboxerfile")
      if -e "debian/$package.Kaboxerfile";
  install_file("debian/$package.kaboxer.yaml", "$tmp/usr/share/kaboxer/$package.yaml")
      if -e "debian/$package.kaboxer.yaml";
  addsubstvar($package, 'misc:Depends', 'docker.io');
}

__END__

=head1 NAME

dh_kaboxer - handle kaboxer

=head1 SYNOPSIS

B<dh_kaboxer> [S<I<debhelper options>>]

=head1 DESCRIPTION

dh_kaboxer is a debhelper program that is responsible for...

A debhelper addon is also provided.  This makes it possible to just use

  #!/usr/bin/make -f
  %:
          dh $@ --with kaboxer

for F<debian/rules>.

=head1 SEE ALSO

L<debhelper(7)>

=head1 COPYRIGHT AND LICENSE

Based on I<dh_perl_openssl> in the I<perl-openssl-defaults> package by Niko Tyni.

Copyright 2010, Ansgar Burchardt <ansgar@debian.org>

Copyright 2016, Niko Tyni <ntyni@debian.org>

Copyright 2019, Roland Mas <lolando@debian.org>

This program is free software, you can redistribute it and/or modify it under
the same terms as Perl itself.
